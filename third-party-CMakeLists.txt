# Note: This file lives in git at 'tlm/third-party-CMakeLists.txt'.
# It is copied by repo into third_party/CMakeLists.txt, so should be
# considered read-only in that location.

# Ensure the third party code is position independent because our
# code is always built as position-independent.
# This setting is specifically required for HdrHistogram_c.
set(CMAKE_POSITION_INDEPENDENT_CODE ON)

set(BENCHMARK_ENABLE_INSTALL OFF)
set(BENCHMARK_ENABLE_TESTING OFF)
add_subdirectory(google-benchmark EXCLUDE_FROM_ALL)

set(gtest_force_shared_crt ON CACHE BOOL
     "Use shared (DLL) run-time lib even when Google Test is built as static lib.")
add_subdirectory(googletest EXCLUDE_FROM_ALL)

add_subdirectory(gsl-lite EXCLUDE_FROM_ALL)
# Migration to gsl-lite v0.36:
if(TARGET gsl::gsl-lite)
  # a) We refer to the old name of gsl-lite - "gsl_lite" in a number of
  # places in CMakeLists.txt. Add an alias to assist in migration.
  add_library(gsl_lite ALIAS gsl-lite)
else()
  # b) the older version (0.25) of gsl-lite requires we add our own
  # include_directories().  There's no explicit version number for
  # gsl-lite exposed at this level, so instead check for the presence
  # of gsl::gsl-lite library (which is only present in 0.36).
  target_include_directories(gsl_lite INTERFACE ${gsl_lite_SOURCE_DIR}/include)
endif()

# Disable building examples and tests for HdrHistogram.
set(HDR_HISTOGRAM_BUILD_PROGRAMS)
add_subdirectory(HdrHistogram_c)

set(SPDLOG_BUILD_EXAMPLES)
set(SPDLOG_BUILD_TESTING)
add_subdirectory(spdlog)

# Define macro which renames spdlog's severity levels
set(CUSTOM_LEVELS "{\"TRACE\",\ \"DEBUG\",\ \"INFO\",\ \"WARNING\",\ \"ERROR\",\ \"CRITICAL\",\ \"OFF\"}")
target_compile_definitions(spdlog INTERFACE -DSPDLOG_LEVEL_NAMES=${CUSTOM_LEVELS})
